
lines {
    #TMP_A_OE
    #TMP_B_OE
    #PC_OE
    #SP_OE
    #PC_LOAD_EN
    PC_CNT_EN
    WE
    RE
    
    #IO_M
    #COND_uJMP
    #RESET
    HALT
    Cin_EN
    ALUOP_0
    ALUOP_1
    ALUOP_2
    
    // these are really active high, this is just a trick
    // to force the default inactive state to be 0xF
    #OE_ENCL_0
    #OE_ENCL_1
    #OE_ENCL_2
    #OE_ENCL_3
    #CLK_ENCL_0
    #CLK_ENCL_1
    #CLK_ENCL_2
    #CLK_ENCL_3

    free2
    free3
    free4
    free5
    free6
    free7
    free8
    free9
}

define {
    // BEGIN: encoded /OE lines
    R0_OE {
        OE_ENCL_0
        OE_ENCL_1
        OE_ENCL_2
        OE_ENCL_3
    }
    R1_OE {
        OE_ENCL_1
        OE_ENCL_2
        OE_ENCL_3
    }
    R2_OE {
        OE_ENCL_0
        OE_ENCL_2
        OE_ENCL_3
    }
    R3_OE {
        OE_ENCL_2
        OE_ENCL_3
    }
    R4_OE {
        OE_ENCL_0
        OE_ENCL_1
        OE_ENCL_3
    }
    R5_OE {
        OE_ENCL_1
        OE_ENCL_3
    }
    R6_OE {
        OE_ENCL_0
        OE_ENCL_3
    }
    FLAGS_SEL {
        OE_ENCL_3
    }

    FLAGS_OE_OPBUS {
        OE_ENCL_0
        OE_ENCL_1
        OE_ENCL_2
    }
    PC_H_OE {
        OE_ENCL_1
        OE_ENCL_2
    }
    PC_L_OE {
        OE_ENCL_0
        OE_ENCL_2
    }
    SP_ALUB_OE {
        OE_ENCL_2
    }
    ALU_OE {
        OE_ENCL_0
        OE_ENCL_1
    }
    // END: encoded /OE lines
    // BEGIN: encoded CLK lines
    R0_CLK_EN {
        CLK_ENCL_0
        CLK_ENCL_1
        CLK_ENCL_2
        CLK_ENCL_3
    }
    R1_CLK_EN {
        CLK_ENCL_1
        CLK_ENCL_2
        CLK_ENCL_3
    }
    R2_CLK_EN {
        CLK_ENCL_0
        CLK_ENCL_2
        CLK_ENCL_3
    }
    R3_CLK_EN {
        CLK_ENCL_2
        CLK_ENCL_3
    }
    R4_CLK_EN {
        CLK_ENCL_0
        CLK_ENCL_1
        CLK_ENCL_3
    }
    R5_CLK_EN {
        CLK_ENCL_1
        CLK_ENCL_3
    }
    R6_CLK_EN {
        CLK_ENCL_0
        CLK_ENCL_3
    }
    FLAGS_CLK_EN {
        CLK_ENCL_3
    }

    SP_CLK_DOWN {
        CLK_ENCL_0
        CLK_ENCL_1
        CLK_ENCL_2
    }
    ALR_CLK {
        CLK_ENCL_1
        CLK_ENCL_2
    }
    SP_CLK_UP {
        CLK_ENCL_0
        CLK_ENCL_2
    }
    SP_LOAD {
        CLK_ENCL_2
    }
    IR_CLK_EN {
        CLK_ENCL_0
        CLK_ENCL_1
    }
    TMP_A_CLK {
        CLK_ENCL_1
    }
    TMP_B_CLK {
        CLK_ENCL_0
    }
    // END: encoded CLK lines
    // BEGIN: ALU OPs
    ALUOP_XOR {
        ALUOP_2
    }
    ALUOP_ADD {
        ALUOP_0
        ALUOP_1
    }
    // END: ALU OPs
    FETCH_T0 {
        PC_OE
        RE
        IR_CLK_EN
        PC_CNT_EN
    }
    FETCH_T1 {
        PC_CNT_EN
        IR_CLK_EN
        RE
    }

    AR_OE {
        PC_OE
        SP_OE
    }

    PCOE_POSTINC_RE {
        PC_OE
        PC_CNT_EN
        RE
    }

    PASS_A {
        TMP_A_OE
        ALU_OE
        ALUOP_XOR
    }
    PASS_B {
        TMP_B_OE
        ALU_OE
        ALUOP_XOR
    }
    ADD_A_B {
        TMP_A_OE
        TMP_B_OE
        ALUOP_ADD
    }

    PUSH_DB {
        SP_OE
        WE
    }
}

instructions {

    // an instriction is always 2 bytes (16 bit):
    // ADDR: 0  1  2  3  4 ..
    // DATA: B0 B1 ..........
    //       |---|
    //    instruction
    //
    // B0: is directly tied to HW decoders to select registers
    // b7 b6 b5 b4 b3 b2 b1 b0
    // |---------| |---------|
    // source 1    source 2 and destination
    // 0b0xxx -> GP regs
    // 0b1xxx -> special regs, ex.: PC, SP, etc..
    //
    // B1:
    // b7 b6 b5 b4 b3 b2 b1 b0
    // |------| |------------|
    // ALU OP   micro code index
    // ALU OP is hardwired to the ALU (Am25LS2517 op codes)
    // micro code index is an address to the micro code ROM

    NOP {
        T0 {
            FETCH_T0
        }
        T1 {
            RESET
        }
    }

    0x08 JMP.S m {
        T0 {
            FETCH_T0
        }
        T1 {
            COND_uJMP
        }
        T2 {
            PCOE_POSTINC_RE
            ALR_CLK
        }
        T3 {
            PC_OE
            RE
            PC_LOAD_EN
        }
        T4 {
            RESET
        }
        // micro counter will jump here if
        // branch miss occured
        T5 {
            PC_CNT_EN
        }
        T6 {
            PC_CNT_EN
        }
        T7 {
            RESET
        }
    }
    JMP.Z m {
        *
    }
    JMP.V m {
        *
    }
    JMP.C m {
        *
    }
    JMP.A m {
        *
    }

    JMP m {
        T0 {
            FETCH_T0
        }
        T1 {
            PCOE_POSTINC_RE
            ALR_CLK
        }
        T2 {
            PC_OE
            RE
            PC_LOAD_EN
        }
        T3 {
            RESET
        }
    }

    CALL m {
        T0 {
            FETCH_T0
        }
        T1 {
            PC_H_OE
            TMP_A_CLK
        }
        T2 {
            PASS_A
            PUSH_DB
        }
        T3 {
            SP_CLK_DOWN
        }
        T4 {
            PC_L_OE
            TMP_A_CLK
        }
        T5 {
            PASS_A
            PUSH_DB
        }
        T6 {
            // put to DB from RAM
            PC_OE
            PC_CNT_EN
            RE
            // load low address
            ALR_CLK
        }
        T7 {
            PC_OE
            RE
            PC_LOAD_EN
            SP_CLK_DOWN
        }
    }

    RET {
        T0 {
            FETCH_T0
        }
        T1 {
            SP_CLK_UP
        }
        T2 {
            SP_OE
            RE
            ALR_CLK            
        }
        T3 {
            SP_CLK_UP
        }
        T4 {
            SP_OE
            RE
            PC_LOAD_EN
        }
        T5 {
            PC_CNT_EN
        }
        T6 {
            PC_CNT_EN
        }
        T7 {
            RESET
        }
    }

    0x10 JMP.NS m {

    }
    JMP.NZ m {

    }
    JMP.NV m {
        
    }
    JMP.NC m {
        
    }
    JMP.NA m {
        
    }

    0x15 MOV r0, r1 {
        T0 {
            FETCH_T0
        }
        T1 {
            R0_OE
            TMP_A_CLK
        }
        T2 {
            PASS_A
            R1_CLK_EN
        }
        T3 {
            RESET
        }
    }

    // load the immediate value m to register rx
    0x39 LD imm, r0 {
        T0 {
            FETCH_T0
        }
        T1 {
            PC_OE
            RE
            PC_CNT_EN
            R0_CLK_EN
        }
        T2 {
            RESET
        }
    }

    0x40 ST r0, m {
        T0 {
            FETCH_T0
        }
        T1 {
            R0_OE
            TMP_A_CLK
        }
        T2 {
            PCOE_POSTINC_RE
            ALR_CLK
        }
        T3 {
            PCOE_POSTINC_RE
            // AHR_CLK implied (0xF is default active line)

        }
        T4 {
            AR_OE
            PASS_A
            WE
        }
        T5 {
            RESET
        }
    }

    ADD r0 {
        T0 {
            FETCH_T0
        }
        T1 {
            R0_OE
            TMP_A_CLK
        }
        T2 {
            R0_OE
            TMP_B_CLK
        }
        T3 {
            ADD_A_B
            FLAGS_CLK_EN
        }
        T4 {
            ADD_A_B
            ALU_OE
            R0_CLK_EN
        }
        T5 {
            RESET
        }
    }
}